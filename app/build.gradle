plugins {
  id 'com.android.application'
  id 'org.jetbrains.kotlin.android'
  id 'com.mikepenz.aboutlibraries.plugin'
}

android {
  namespace 'com.anissan.bpn'
  buildToolsVersion '33.0.1'
  compileSdkVersion 33

  defaultConfig {
    minSdkVersion 21
    targetSdkVersion 33
    versionCode 1
    versionName '0.1.0'

    // Generating "BuildConfig.*" properties
    Properties buildConfigProperties = new Properties()
    buildConfigProperties.load(new FileInputStream(rootProject.file('build-config.properties')))
    buildConfigProperties.each { key, value -> { buildConfigField('String', key, "\"$value\"") } }
    // Values read as is; they need to be "quoted" to become strings

    // Generating "@string/*" resource values
    Properties resourceConfig = new Properties()
    resourceConfig.load(new FileInputStream(rootProject.file('res-value.properties')))
    resourceConfig.each { key, value -> { resValue('string', key, value) } }
  }

  buildTypes {
    debug {
      applicationIdSuffix '.debug'
      versionNameSuffix '-debug'
    }

    release {
      minifyEnabled true
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
    }
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_11
    targetCompatibility JavaVersion.VERSION_11
  }

  kotlinOptions {
    jvmTarget = '11'
  }

  buildFeatures {
    viewBinding true
  }
}

dependencies {
  implementation 'org.jetbrains.kotlin:kotlin-stdlib:1.8.0'
  implementation 'org.jetbrains:annotations:24.0.0'

  implementation 'androidx.appcompat:appcompat:1.7.0-alpha02'
  implementation 'androidx.core:core-ktx:1.9.0'
  implementation 'androidx.fragment:fragment-ktx:1.6.0-alpha05'
  implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
  implementation 'androidx.viewpager2:viewpager2:1.1.0-beta01'
  implementation 'com.google.android.material:material:1.9.0-alpha01'

  // Needed com.google.common.util.concurrent.ListenableFuture in PermissionTabViewHolder
  implementation 'com.google.guava:guava:31.1-android'

  implementation 'io.insert-koin:koin-android:3.3.3'
  implementation 'com.squareup.okhttp3:okhttp:5.0.0-alpha.10'
  implementation 'io.github.g00fy2.quickie:quickie-bundled:1.6.0'
  implementation 'dev.chrisbanes.insetter:insetter:0.6.1'
  implementation 'com.github.judemanutd:autostarter:1.1.0'
  implementation 'com.mikepenz:aboutlibraries:10.6.0-rc02'
  implementation 'hu.autsoft:krate:2.0.0'

  // Using an unreleased feature from this version
  implementation 'dev.doubledot.doki:library:c18efeb'
}
