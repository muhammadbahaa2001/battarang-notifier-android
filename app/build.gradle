plugins {
  id 'com.android.application'
  id 'org.jetbrains.kotlin.android'
  id 'com.mikepenz.aboutlibraries.plugin'
}

android {
  namespace 'com.anissan.battarang'
  buildToolsVersion '33.0.1'
  compileSdkVersion "android-UpsideDownCake"

  defaultConfig {
    minSdkVersion 21
    targetSdkVersion 33
    versionCode 1
    versionName '0.1.0'

    // Generate "BuildConfig.*" properties
    Properties buildConfigProperties = new Properties()
    buildConfigProperties.load(new FileInputStream(rootProject.file('local.properties')))

    // Skip auto generated Gradle value
    buildConfigProperties.remove('sdk.dir')

    // Values loaded as characters; adding quotes to make them strings
    buildConfigProperties.each { key, value -> { buildConfigField('String', key, "\"$value\"") } }
  }

  buildTypes {
    debug {
      applicationIdSuffix '.debug'
      versionNameSuffix '-debug'
    }

    release {
      minifyEnabled true
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
    }
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_11
    targetCompatibility JavaVersion.VERSION_11
  }

  kotlinOptions {
    jvmTarget = '11'
  }

  buildFeatures {
    viewBinding true
  }
}

dependencies {
  implementation 'org.jetbrains.kotlin:kotlin-stdlib:1.8.20'
  implementation 'org.jetbrains:annotations:24.0.1'

  implementation 'androidx.appcompat:appcompat:1.7.0-alpha02'
  implementation 'androidx.core:core-ktx:1.10.0'
  implementation 'androidx.fragment:fragment-ktx:1.6.0-alpha09'
  implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
  implementation 'androidx.viewpager2:viewpager2:1.1.0-beta01'
  implementation 'com.google.android.material:material:1.10.0-alpha01'

  // `com.google.common.util.concurrent.ListenableFuture` used in PermissionTabViewHolder
  implementation 'com.google.guava:guava:31.1-android'

  implementation 'io.insert-koin:koin-android:3.4.0'
  implementation 'com.squareup.okhttp3:okhttp:5.0.0-alpha.10'
  implementation 'io.github.g00fy2.quickie:quickie-bundled:1.6.0'
  implementation 'dev.chrisbanes.insetter:insetter:0.6.1'
  implementation 'com.github.judemanutd:autostarter:1.1.0'
  implementation 'com.mikepenz:aboutlibraries:10.6.2'
  implementation 'hu.autsoft:krate:2.0.0'

  // Using an unreleased feature from this version
  implementation 'dev.doubledot.doki:library:c18efeb'
}
